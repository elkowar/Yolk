//! Pest grammar for handlebars templating
WHITESPACE = _{ " " | "\t" }
YolkFile   = ${ SOI ~ Element+ ~ EOI }

Element = _{ Block | Raw }

Block        = _{ IfBlock | DirectiveTag }
IfBlock      =  { (#if = IfTag) ~ (#body = Raw?) ~ ((#else = ElseTag) ~ (#elsebody = Raw?))? ~ (#end = EndTag) }
IfTag        =  { BeforeTag ~ "if" ~ #pred = TagInner ~ AfterTag }
ElseTag      =  { BeforeTag ~ "else" ~ AfterTag }
EndTag       =  { BeforeTag ~ "end" ~ AfterTag }
DirectiveTag =  { BeforeTag ~ #name = DirectiveName ~ wsp* ~ #value = TagInner ~ wsp* ~ AfterTag }

DirectiveName = { "CommentPrefix" }

nl  = _{ NEWLINE }
wsp = _{ WHITESPACE }

TagStart  = _{ "{%" }
TagEnd    = _{ "%}" }
BeforeTag = _{ (!(TagStart | nl) ~ ANY)* ~ TagStart ~ wsp* }
AfterTag  = _{ wsp* ~ TagEnd ~ (!(nl | EOI) ~ ANY)* ~ (nl | EOI) }
TagInner  =  { (!TagEnd ~ ANY)+ }

Raw = { (!BeforeTag ~ ANY)+ }
