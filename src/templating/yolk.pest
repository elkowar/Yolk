WHITESPACE = _{ " " | "\t" }

nl = { NEWLINE }
// wsp = { WHITESPACE }
// ws  = { wsp* }
any = { ANY }

InlineTag    = !{ "{<" ~ InlineTagInner ~ ">}" }
MultiLineTag = !{ "{%" ~ MultiLineTagInner ~ "%}" }
NextLineTag  = !{ "{#" ~ NextLineTagInner ~ "#}" }

MultiLineTagInner        = _{ #kind = (MultiLineTagEndInner | MultiLineTagElseIfInner | MultiLineTagIfInner | MultiLineTagElseInner | MultiLineTagRegularInner) }
MultiLineTagRegularInner =  { (!("%}" | nl) ~ ANY)* }
MultiLineTagElseInner    =  { "else" }
MultiLineTagEndInner     =  { "end" }
MultiLineTagIfInner      =  { "if" ~ #expr = MultiLineTagRegularInner }
MultiLineTagElseIfInner  =  { "elif" ~ #expr = MultiLineTagRegularInner }

InlineTagInner        = { #kind = (InlineTagIfInner | InlineTagRegularInner) }
InlineTagRegularInner = { (!(">}" | nl) ~ ANY)* }
InlineTagIfInner      = { "if" ~ #expr = InlineTagRegularInner }

NextLineTagInner        = { #kind = (NextLineTagIfInner | NextLineTagRegularInner) }
NextLineTagRegularInner = { (!("#}" | nl) ~ ANY)* }
NextLineTagIfInner      = { "if" ~ #expr = NextLineTagRegularInner }

TagEnd = @{ (!nl ~ any)* ~ (nl | EOI) }

LineInlineTag      = ${ #left = LineInlineTagStart ~ #tag = InlineTag ~ #right = TagEnd }
LineInlineTagStart = @{ (!(nl | "{#" | "{%" | "{<") ~ any)* }

LineNextLineTag      = ${ #left = LineNextLineTagStart ~ #tag = NextLineTag ~ #right = TagEnd }
LineNextLineTagStart = @{ (!(nl | "{#" | "{%" | "{<") ~ any)* }

LineMultiLineTag      = ${ #left = LineMultiLineTagStart ~ #tag = MultiLineTag ~ #right = TagEnd }
LineMultiLineTagStart = @{ (!(nl | "{#" | "{%" | "{<") ~ any)* }

// We have the alternative here to ensure that when this is the last line, it at least contains SOMETHING.
// Otherwise, this rule would accept the empty string
Raw = @{ ((!nl ~ any)* ~ nl) | ((!nl ~ any)+ ~ EOI) }

Line     = _{ LineMultiLineTag | LineNextLineTag | LineInlineTag | Raw }
Document = _{ Line* }
